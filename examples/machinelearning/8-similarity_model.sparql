prefix : <http://schema.org/>
prefix spa: <tag:stardog:api:analytics:>

INSERT {
    graph spa:model {
        :s1 a spa:SimilarityModel ;
            spa:arguments (?genres ?directors ?authors ?producers ?metaCritic) ;
            spa:predict ?movie ;
            spa:overwrite True .
    }
}
WHERE {
    SELECT 
    (agg:spa:set(?genre) as ?genres) 
    (agg:spa:set(?director) as ?directors)
    (agg:spa:set(?author) as ?authors)
    (agg:spa:set(?producer) as ?producers)
    ?metaCritic
    ?movie
    {
        ?movie  :genre ?genre ;
                :director ?director ;
                :author ?author .

        OPTIONAL {
            ?movie  :productionCompany ?producer .
        }

        OPTIONAL {
            ?movie  :metaCritic ?metaCritic .
        }
    }
    GROUP BY ?movie ?metaCritic 
}
