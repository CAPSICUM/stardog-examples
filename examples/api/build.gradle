group = "com.complexible.stardog.examples.api"

dependencies {
	compile ("org.openrdf.sesame:sesame-runtime:2.7.12") {
		exclude group: "org.slf4j"
		exclude group: "com.fasterxml.jackson.core"
	}
	compile (group: "com.google.guava", name: "guava", version: "17.0") {
		exclude group: "com.google.code.findbugs"
	}

	stardogClientLibs.each { d ->
		compile fileTree(dir: d, include: '*.jar')
	}

	stardogServerLibs.each { d ->
		compile fileTree(dir: d, include: '*.jar')
	}
}

task execute(type:JavaExec) {

	if (project.hasProperty("mainClass")) {
		main = project.mainClass
	}
	else {
		main = "com.complexible.stardog.examples.api.ConnectionAPIExample"
	}

	classpath = sourceSets.main.runtimeClasspath

	if (project.hasProperty("stardogHome")) {
		jvmArgs = [ "-Dstardog.home=${project.stardogHome}" ]
	}
	else {
		jvmArgs = [ "-Dstardog.home=${project.stardogPath}" ]
	}
}

task executeAll << {
	sourceSets.main.allJava.each { File aFile ->
		runExample aFile.getAbsolutePath()
	}
}

task docs << {
	exec {
		commandLine "docco",
					"-o",
					"${distPath}/docs",
					"${srcPath}/com/complexible/stardog/examples/api/ConnectionAPIExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/ConnectionPoolsExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/ExplanationExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/WaldoAPIExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/VersioningExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/ReasoningExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/ProvSkosExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/ICVExample.java",
					"${srcPath}/com/complexible/stardog/examples/sesame/SesameExample.java",
					"${srcPath}/com/complexible/stardog/examples/jena/JenaExample.java"
	}
}

def runExample(String example) {
	// replace / with .
	String fqcn = example.replaceAll(System.getProperty("file.separator"), ".");
	// strip .java
	fqcn = fqcn.substring(0, fqcn.length() - 5);
	fqcn = fqcn.substring(fqcn.indexOf("com.complexible"));

	javaexec {

		main = fqcn

		classpath = project(':examples:api').sourceSets.main.runtimeClasspath

		if (project.hasProperty("stardogHome")) {
			jvmArgs = [ "-Dstardog.home=${project.stardogHome}" ]
		}
		else {
			jvmArgs = [ "-Dstardog.home=${project.stardogPath}" ]
		}
	}
}